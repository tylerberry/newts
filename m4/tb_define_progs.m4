# tb_define_progs.m4                                  -*- Autoconf -*-
# detect a particular executable and AC_DEFINE it.
# Copyright (C) 2003 Tyler Berry.
#
# Based on AC_PATH_PROGS and AC_PATH_PROG from Autoconf.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
# 02111-1307, USA.
#
# As a special exception, the author gives unlimited permission to
# copy, distribute, and modify the portion of a configure script
# corresponding to these macros generated by Autoconf.  You need not
# follow the terms of the GNU General Public License when using or
# distributing such scripts, even though portions of these macros
# appear in them.  The GNU General Public License (GPL) does govern
# all other use of the material that constitutes these macros.
#
# Certain portions of the macro source text are designed to be copied
# (in certain cases, depending on the input) into the output of
# Autoconf.  We call these the "data" portions.  The rest of the
# macro source text consists of comments plus executable code that
# decides which of the data portions to output in any given case.  We
# call these comments and executable code the "non-data" portions.
# Autoconf never copies any of the non-data portions into its output.
#
# This special exception to the GPL applies to these macros and to
# versions of Autoconf released by the Free Software Foundation.
# When you make and distribute modified versions of these pieces of
# software, you may extend this special exception to the GPL to apply
# to your modified version as well, *unless* your modified version
# has the potential to copy into its output some of the text that was
# the non-data portion of the version you started with.  (In other
# words, unless your change moves or copies text from the non-data
# portions to the data portions.)  If your modification has such
# potential, you must delete any notice of this special exception to
# the GPL from your modified version.

# tb_DEFINE_PROGS(VARIABLE, PROGS-TO-CHECK-FOR, [VALUE-IF-NOT-FOUND],
#                 [PATH])
# ------------------------------------------------------------------
AC_DEFUN([tb_DEFINE_PROGS],
[for ac_prog in $2
do
  tb_DEFINE_PROG([$1], [$ac_prog], , [$4])
  test -n "$ac_cv_prog_$1" && break
done
m4_ifvaln([$3], [test -n "$ac_cv_prog_$1" || $1="$3"])])

# tb_CHECK_PROG(VARIABLE, PROG-TO-CHECK-FOR,
#               [VALUE-IF-NOT-FOUND], [PATH])
# -----------------------------------------------------
AC_DEFUN([tb_DEFINE_PROG],
[# Extract the first word of "$2", so it can be a program name with args.
set dummy $2; ac_word=$[2]
AC_MSG_CHECKING([for $ac_word])

_AS_PATH_WALK([$4],
[for ac_exec_ext in '' $ac_executable_extensions; do
  if AS_EXECUTABLE_P(["$as_dir/$ac_word$ac_exec_ext"]); then
    ac_cv_prog_$1="$as_dir/$ac_word$ac_exec_ext"
    echo "$as_me:$LINENO: found $as_dir/$ac_word$ac_exec_ext" >&AS_MESSAGE_LOG_FD
    break 2
  fi
done])

dnl If no 3rd arg is given, leave the cache variable unset,
dnl so tb_DEFINE_PROGS will keep looking.

m4_ifvaln([$3],
[  test -z "$ac_cv_prog_$1" && ac_cv_prog_$1="$3"])dnl

if test -n "$ac_cv_prog_$1"; then
  AC_DEFINE_UNQUOTED($1,"$ac_cv_prog_$1")
  AC_MSG_RESULT([$ac_cv_prog_$1])
else
  AC_MSG_RESULT([no])
fi
])# tb_DEFINE_PROG
